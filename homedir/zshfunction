# Remove a line from history.
# Accepts one history line number as argument.
# Use `dc -1` to remove the last line.
remove-historyline() {
    if [[ $# -ne 1 ]]; then
        print "Usage: remove-historyline <history line number>"
        return 1
    fi

    # Prevent the specified history line from being saved.
    local HISTORY_IGNORE="${(b)$(fc -ln $1 $1)}"

    # Write out the history to file, excluding lines that match
    # `$HISTORY_IGNORE`.
    fc -W

    # Dispose of the current history and read the new history from file.
    fc -p $HISTFILE $HISTSIZE $SAVEHIST

    print "Deleted line ${1} from history."
}

# Enable processing items in a list that has a header without processing the
# header, by splitting the header from the body of a list, and passing on the
# rest the lines for processing.
list-body() {
	IFS= read -r header     # Capture the header.
	printf '%s\n' "$header" # Print the header.
	"$@"                    # Return the remaining lines.
}

